// Copyright 2020-2023, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

buildscript {
    ext {
        kotlinVersion = '1.7.10'

        latestAboutLibsRelease = '8.9.4'

        androidxCoreVersion = '1.8.0'
        androidxAnnotationVersion = '1.3.0'
        androidxAppCompatVersion = '1.5.1'
        androidxLifecycleVersion = '2.5.1'
        androidxConstraintLayoutVersion = '2.1.3'
        androidxCardViewVersion = '1.0.0'
        androidxRecyclerViewVersion = '1.2.1'

        hiltVersion = '2.45'

        materialVersion = '1.8.0'

        // This is the version to download if we can't find it locally.
        eigenFetchVersion = '3.4.0'

        // If you update this, must also update .gitlab-ci/config.yml
        buildToolsVersion = '32.0.0'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:${latestAboutLibsRelease}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
    }
}
plugins {
    // Android Gradle Plugin
    id 'com.android.application' version '8.1.0' apply false
    id 'com.android.library' version '8.1.0' apply false

    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false

    // Used for getting the eigen dir, explicit python binary, etc. from local.properties
    id 'com.github.b3er.local.properties' version '1.1'

    // For getting git describe data and formatting it how Android wants.
    id "com.gladed.androidgitversion" version "0.4.14"

    // For downloading e.g. the CDDL (for transitive dependencies of hilt)
    id "de.undercouch.download" version "4.1.2"

    // So we don't have to check in generated files: we start with SVGs for vector art.
    id "com.quittle.svg-2-android-vector" version "0.1.0" apply false

    // Spotless for Java and Kotlin's code formatting
    id 'com.diffplug.spotless' version "6.22.0"
}

spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}

ext {
    ndk_version = '21.4.7075529'
    // If you update this, must also update .gitlab-ci/config.yml
    sharedCompileSdk = 32
    // If targetSDk > 28 && android-running-host >= 30, reading shared files from external storage is disallowed,
    // (except for a special case uses-permission MANAGE_EXTERNAL_STORAGE)
    sharedTargetSdk = 28
    sharedMinSdk = 26

    // If you are building on Windows, you will need to explicitly set eigenIncludeDir in your
    // local.properties file since the default value provided below only makes sense on *nix
    eigenIncludeDir = project.findProperty('eigenIncludeDir') ?: '/usr/include/eigen3'

    // If you're having trouble with a "can't find python" CMake error, you can specify the path to
    // Python 3 explicitly in local.properties with a property named "pythonBinary"
    pythonBinary = project.findProperty('pythonBinary')
}
